#!/usr/bin/perl
#
# TODO: Wire this up to an Action workflow
#
# ./bin/remove_old_ads < docs/perl_ads.json > docs/perl_ads.json.new
# mv docs/perl_ads.json.new docs/perl_ads.json
# git commit ...

use strict;
use warnings;

use feature qw(say fc);

use JSON;
use Time::Piece;

my $parser = JSON->new->utf8(1);

my $json = do { local $/; <> };

my $ads = $parser->decode($json);

my $today = localtime->ymd;

my @filtered_ads = grep {
  ! exists $_->{end} or ! defined $_->{end} or $_->{end} ge $today;
} @$ads;

my @sorted_ads = sort {
  fc($a->{description} // '') cmp fc($b->{description} // '')
} @filtered_ads;

# Output canonicalised JSON (keys sorted, pretty, UTF-8)
say $parser->encode(\@sorted_ads);
